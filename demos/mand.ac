#main settings
let maxitter 250
let asciiWidth 250 # 250
let asciiHeight 125 # 73

# -0.5 0 1.5 (full mandelbrot)
# -0.7453 -0.1127 0.00065 (spiral)
# -0.235125 -0.827215 0.00004(crack)

#positional settings
let x 0
let y 0
let zoom 2

#setting caluculation

let minx x
let maxx x
sub minx zoom
add maxx zoom

let miny y
let maxy y
sub miny zoom
add maxy zoom

let dx zoom
mul dx 2
div dx asciiWidth

let dy zoom
mul dy 2
div dy asciiHeight

#main loop
let real minx
let imag miny
:drawMandelbrot
:nextCol
:nextRow
goto printnum
:back
add real dx
gotoiflss real maxx nextRow
let real minx
add imag dy
print "\n"
gotoiflss imag maxy nextCol
exit 0

#calculation per cell
:printnum
let i 0
let z 0
let nextreal real
let nextimag imag
:comptest_loop
let aa nextreal
pow aa 2
let subaa nextimag
pow subaa 2
sub aa subaa
let bb 2
mul bb nextimag
mul bb nextreal
let nextreal aa
add nextreal real
let nextimag bb
add nextimag imag
let magnitude nextreal
add magnitude nextimag
gotoifgtr magnitude 16 printfilled
gotoiflss magnitude -16 printfilled
more i
gotoiflss i maxitter comptest_loop
goto printspace

#print functions
:printfilled
print "@"
goto back

:printspace
print "."
goto back

exit 1